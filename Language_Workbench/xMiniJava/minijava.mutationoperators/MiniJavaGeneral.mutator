generate exhaustive mutants
in "data/out/"
from "data/model/"
metamodel "/MiniJavaMutation/data/model/XMiniJava.ecore"

with blocks {
	cs "Changes the symbol of symbolRef with another symbol of the same type"{
		vd1 = select one VariableDeclaration
		vd2 = select one VariableDeclaration where {self <> vd1 and typeRef = vd1->typeRef}
		modify one SymbolRef where {symbol = vd1} with {symbol = vd2}
	}
	nrst "Null ReturnStatment" {
		rtstmt = select one Return
		create Null in rtstmt->expression
	}
	cbct "Changes the value of boolean constant: true"{
		modify one BoolConstant where {value = 'false'} with {value = 'true'}
	}
	cbcf "Changes the value of boolean constant: false"{
		modify one BoolConstant where {value = 'true'} with {value = 'false'}
	}
	cscr "Changes the value of string constant: random string"{
		modify one StringConstant with {value = random-string(1, 4)}
	}
	csce "Changes the value of string constant: empty string"{
		modify one StringConstant where {value <> ""} with {value = ""}
	}
	cicr "Changes the value of integer constant: random int"{
		modify one IntConstant with {value = random-int}
	}
	cic0 "Changes the value of integer constant: 0"{
		modify one IntConstant where {value <> 0} with {value = 0}
	}
	cic1 "Changes the value of integer constant: 1"{
		modify one IntConstant where {value <> 1} with {value = 1}
	}
	cicminus1 "Changes the value of integer constant: -1"{
		modify one IntConstant where {value <> -1} with {value = -1}
	}
}